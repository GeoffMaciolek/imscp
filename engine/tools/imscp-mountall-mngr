#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2015 by internet Multi Server Control Panel
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.

# Script that mount homedirs and httpd logs folders on reboot

use FindBin;
use lib "$FindBin::Bin/..", "$FindBin::Bin/../PerlLib", "$FindBin::Bin/../PerlVendor";
use iMSCP::Debug;
use iMSCP::Bootstrapper;
use iMSCP::Database;
use iMSCP::EventManager;
use iMSCP::Getopt;
use iMSCP::Mail;
use iMSCP::Mount qw/mount umount/;
use Servers::httpd;
use File::Basename;

sub mountHomedirs
{
	my $eventManager = iMSCP::EventManager->getInstance();
	my $dbh = iMSCP::Database->factory()->getRawDb();
	my $sth = $dbh->prepare("SELECT domain_name FROM domain WHERE domain_status IN('ok', 'disabled')");
	$sth or die(sprintf('Could not prepare SQL statement: %s' , $dbh->errstr));
	$sth->execute() or die(sprintf('Could not execute prepared statement: %s', $dbh->errstr));

	while (my $row = $sth->fetchrow_hashref()) {
		my $homedir = "$main::imscpConfig{'USER_WEB_DIR'}/$row->{'domain_name'}";
		my $mountOptions = { fs_spec => $homedir, fs_file => $homedir, fs_vfstype => 'none', fs_mntops => 'shared,bind' };
		$eventManager->trigger('beforeMountHomedir', $mountOptions);
		mount($mountOptions);
		$eventManager->trigger('afterMountHomedir', $mountOptions);
	}
}

sub mountLogFolders
{
	my $eventManager = iMSCP::EventManager->getInstance();
	my $httpd = Servers::httpd->factory();
	my $dbh = iMSCP::Database->factory()->getRawDb();
	my $sth = $dbh->prepare(
		"
			SELECT domain_name, domain_name AS root_domain_name FROM domain WHERE domain_status IN('ok', 'disabled')
			UNION
			SELECT
				alias_name, domain_name AS root_domain_name
			FROM
				domain_aliasses
			INNER JOIN
				domain USING(domain_id)
			WHERE
				alias_status IN('ok', 'disabled')
			UNION
			SELECT
				CONCAT(subdomain_name, '.', domain_name), domain_name AS root_domain_name
			FROM
				subdomain
			JOIN
				domain USING(domain_id)
			WHERE
				subdomain_status IN('ok', 'disabled')
			UNION
			SELECT
				CONCAT(subdomain_alias_name, '.', alias_name), domain_name AS root_domain_name
			FROM
				subdomain_alias
			INNER JOIN
				domain_aliasses USING(alias_id)
			INNER JOIN
				domain USING(domain_id)
			WHERE
				subdomain_alias_status IN('ok', 'disabled')
		"
	);
	$sth or die(sprintf('Could not prepare SQL statement: %s' , $dbh->errstr));
	$sth->execute() or die(sprintf('Could not execute prepared statement: %s', $dbh->errstr));

	while (my $row = $sth->fetchrow_hashref()) {
		$httpd->mountLogsFolder({
			DOMAIN_NAME =>$row->{'domain_name'},
			HOME_DIR => "$main::imscpConfig{'USER_WEB_DIR'}/$row->{'root_domain_name'}"
		});
	}
}

newDebug('imscp-mountall-mngr.log');

iMSCP::Getopt->parseNoDefault(sprintf("Usage: perl %s [OPTION]...", basename($0)) . qq {

Mounts the customer home directories and their httpd log folders

OPTIONS:
 -v,    --verbose       Enable verbose mode.},
 'verbose|v' => sub { setVerbose(@_); }
);

iMSCP::Bootstrapper->getInstance()->boot({ norequirements => 1, nolock => 1, config_readonly => 1, mode => 'reboot' });

eval {
	mountHomedirs();
	mountLogFolders() if Servers::httpd->can('mountLogsFolder');
	my @errorMessages = getMessageByType('error');
};

if($@) {
	my $error = $@;
	iMSCP::Mail->new()->errmsg($error);
	die($error);
};
